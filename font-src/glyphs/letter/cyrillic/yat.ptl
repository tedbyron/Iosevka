$$include '../../../meta/macros.ptl'

import [mix linreg clamp fallback] from"../../../support/utils.mjs"

glyph-module

glyph-block Letter-Cyrillic-Yat : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Letter-Shared-Metrics : BowlXDepth
	glyph-block-import Letter-Shared-Shapes : LetterBarOverlay
	glyph-block-import Letter-Cyrillic-Yeri : CyrlYeriUprightShape

	define [xBarLeft df] : Math.max (df.rightSB - (RightSB - SB)) : if SLAB
		Just ([mix df.leftSB df.rightSB 0.35] - df.mvs / 2 * HVContrast)
		Just  [mix df.leftSB df.rightSB 0.2]

	define [YatShape df top pBar fLowerCase] : glyph-proc
		local xYeriLeft : xBarLeft df

		include : CyrlYeriUprightShape top
			left -- xYeriLeft
			right  -- df.rightSB - O * 2
			pBar -- pBar

		local cTop : if SLAB (top - Stroke / 2) top

		local xCrossbarLeft : mix 0 df.leftSB [if SLAB 0.25 0.375]
		local xCrossbarRight : mix xCrossbarLeft (xYeriLeft + 0.5 * HVContrast * Stroke) 2
		local yCrossbar : [mix (top * pBar + HalfStroke) cTop [if SLAB 0.625 0.5]] + 0.25 * OverlayStroke
		include : HBar.t xCrossbarLeft xCrossbarRight yCrossbar OverlayStroke

		if SLAB : begin
			local sw : Math.min (VJutStroke * df.mvs / Stroke) (0.625 * (xYeriLeft - xCrossbarLeft))
			local vJut : Math.min VJut (OverlayStroke + 0.5 * (yCrossbar - OverlayStroke - (top * pBar)))
			include : VSerif.dl xCrossbarLeft  yCrossbar vJut sw
			include : VSerif.dr xCrossbarRight yCrossbar vJut sw

			if fLowerCase : begin
				eject-contour 'serifYeriLT'
				include : tagged 'serifLT' : HSerif.lt xYeriLeft Ascender SideJut

	create-glyph 'cyrl/Yat' 0x462 : glyph-proc
		local df : DivFrame [mix 1 para.diversityM 0.5]
		set-width df.width
		include : df.markSet.capital
		include : YatShape df CAP 0.5 false

	create-glyph 'cyrl/yat.upright' : glyph-proc
		local df : DivFrame [mix 1 para.diversityM 0.5]
		set-width df.width
		include : df.markSet.b
		include : YatShape df Ascender 0.375 true

	glyph-block-import Letter-Shared-Shapes : nShoulderKnots
	create-glyph 'cyrl/yat.italic' : glyph-proc
		local df : DivFrame para.diversityM 3
		local fine : AdviceStroke 4 df.div

		local xMidBarRight   : df.middle + 0.5 * df.mvs * HVContrast
		local xRightLoopLeft : df.rightSB - fine * HVContrast - OX
		local rinner         : (xRightLoopLeft - xMidBarRight) / 2

		set-width df.width
		include : df.markSet.e
		include : VBar.l df.leftSB 0 XH df.mvs
		include : dispiro
			nShoulderKnots
				left   -- (df.leftSB + df.mvs * HVContrast)
				right  -- xMidBarRight
				fine   -- ShoulderFine
				bottom -- (rinner + fine)
				stroke -- df.mvs
			arcvh
			g4 [mix xMidBarRight xRightLoopLeft 0.5] (fine + O) [widths.rhs fine]
			archv
			g4 xRightLoopLeft (rinner + fine)
			arcvh
			g4 [mix xMidBarRight xRightLoopLeft 0.5] (rinner * 2 + fine - O)
			archv
			g4.down.end xMidBarRight (rinner + fine) (widths.rhs)

		if SLAB : begin
			if [not para.isItalic] : include : tagged 'serifLB'
				HSerif.lb df.leftSB 0  SideJut
			include : tagged 'serifLT' : HSerif.lt    df.leftSB XH SideJut
